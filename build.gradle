buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "java"
	id "application"
	id "jvm-test-suite"

    id "jacoco"
    id "jacoco-report-aggregation"

    id "com.github.spotbugs" version "6.2.1"
    id "pmd"
}

java {
    sourceCompatibility = JavaVersion.toVersion(17)
    targetCompatibility = JavaVersion.toVersion(17)
}

repositories {
	mavenCentral()
}

dependencies {
	// Dependencies for code in src/main
	// implementation "jsoup...

	// Dependencies for code in src/test
    // Now handled in testing block
}

jar {
	archiveBaseName = "GeneratePrimes"

    manifest {
        attributes(
            "Main-Class": "edu.odu.cs.cs350.examples.numbers.RunPrimeGenerator"
        )
    }
}

application {
	mainClass = "edu.odu.cs.cs350.examples.numbers.RunPrimeGenerator"
}

testing {
	suites {
        configureEach {
			useJUnitJupiter()

            dependencies {
                implementation project()
                implementation "org.junit.jupiter:junit-jupiter:5.8.2"
                implementation "org.hamcrest:hamcrest-library:2.2"
            }
        }

		test {
            // common settings in "configureEach" block
		}

        integrationTest(JvmTestSuite) {
            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
	}
}

test {
    ignoreFailures = true

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }

    finalizedBy jacocoTestReport
}

integrationTest {
    ignoreFailures = true

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }

    finalizedBy integrationTestCodeCoverageReport
}

// Update "check" task to include integration test set
tasks.named("check") {
    dependsOn(testing.suites.integrationTest)
}

//------------------------------------------------------------------------------
// Documentation Tool(s)
//------------------------------------------------------------------------------
javadoc {
    failOnError = false
}

//------------------------------------------------------------------------------
// Analysis Tools
//------------------------------------------------------------------------------
jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = true
    }
}

// SpotBugs Config
spotbugs {
    toolVersion = "4.9.3"
}

spotbugsMain {
    ignoreFailures = true
    effort = com.github.spotbugs.snom.Effort.MAX
    reportLevel = com.github.spotbugs.snom.Confidence.values()[1] // Medium
    reports {
       html.required = true
       xml.required = false
    }
}

spotbugsTest {
    enabled = false
}

spotbugsIntegrationTest {
    enabled = false
}
// End SpotBugs Config

// PMD Config
pmd {
    toolVersion = "7.0.0"

    ignoreFailures = true
    ruleSets = [
        "category/java/bestpractices.xml",
        "category/java/codestyle.xml",
        "category/java/design.xml",
        "category/java/errorprone.xml",
        "category/java/performance.xml"
    ]
}

pmdTest {
    enabled = false
}

pmdIntegrationTest {
    enabled = false
}
// End PMD Config


